apply plugin: eclipsebuild.TestBundlePlugin
apply plugin: 'groovy'

dependencies {
    compile project(':org.eclipse.buildship.core')
    compile project(':org.eclipse.buildship.ui')
    compile withEclipseBundle('org.eclipse.swtbot.eclipse.finder')
    compile withEclipseBundle('org.eclipse.swtbot.junit4_x')

    bundled "org.spockframework:spock-core:$spockLibVersion"
    bundled "cglib:cglib-nodep:$cglibLibVersion"
    bundled "com.gradleware.tooling:testing-junit:$commonsLibVersion"
    bundled "org.slf4j:slf4j-simple:$slf4jLibVersion"
}

eclipseTest {
    fragmentHost 'org.eclipse.buildship.ui'
    applicationName 'org.eclipse.swtbot.eclipse.core.swtbottestapplication'
    optionsFile rootProject.project(':org.eclipse.buildship.core').file('.options')
    consoleLog = true
}

// append the sources of each first-level dependency and its transitive dependencies of
// the 'bundled' configuration to the 'bundledSource' configuration
configurations.bundled.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
    addSourcesRecursively(dep)
}

def addSourcesRecursively(dep) {
    dependencies {
        bundledSource group: dep.moduleGroup, name: dep.moduleName, version: dep.moduleVersion, classifier: 'sources'
    }
    dep.children.each { addSourcesRecursively(it) }
}

